# tool: scribe-data
# All Spanish (Q1321) nouns (Q1084) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT DISTINCT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?singular
  (GROUP_CONCAT(DISTINCT ?plural; separator=", ") AS ?plurals)
  ?feminineSingular
  ?femininePlural

  ?masculineSingular
  ?masculinePlural
  (GROUP_CONCAT(DISTINCT ?gender; separator=", ") AS ?genders)
WHERE {
  ?lexeme dct:language wd:Q1321 ;
    wikibase:lexicalCategory wd:Q1084 ;
    wikibase:lemma ?singular .

  # MARK: Plural

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralForm .
    ?pluralForm ontolex:representation ?plural ;
      wikibase:grammaticalFeature wd:Q146786 .
  }

  # MARK: Feminine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineSingularForm .
    ?feminineSingularForm ontolex:representation ?feminineSingular ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?femininePluralForm .
    ?femininePluralForm ontolex:representation ?femininePlural ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q146786 .
  }

  # MARK: Masculine

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculineSingularForm .
    ?masculineSingularForm ontolex:representation ?masculineSingular ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q110786 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?masculinePluralForm .
    ?masculinePluralForm ontolex:representation ?masculinePlural ;
      wikibase:grammaticalFeature wd:Q499327, wd:Q146786 .
  }

  # MARK: Gender(s)

  OPTIONAL {
    ?lexeme wdt:P5185 ?nounGender .
  }

  SERVICE wikibase:label {
    bd:serviceParam wikibase:language "en" .
    ?nounGender rdfs:label ?gender .
  }
}
GROUP BY ?lexeme ?singular ?feminineSingular ?femininePlural ?masculineSingular ?masculinePlural
