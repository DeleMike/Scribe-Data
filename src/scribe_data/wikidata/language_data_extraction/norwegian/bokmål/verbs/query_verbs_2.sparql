# tool: scribe-data
# All Bokm√•l (Q25167) verbs (Q24905) and the given forms.
# Enter this query at https://query.wikidata.org/.

SELECT
  (REPLACE(STR(?lexeme), "http://www.wikidata.org/entity/", "") AS ?lexemeID)
  ?lastModified
  ?presentAndParticiple
  ?pluralPastAndParticiple
  ?definiteSingularPastAndParticiple
  ?neuterIndefiniteSingularPastAndParticiple
  ?feminineMasculineIndefiniteSingularPastAndParticiple

WHERE {
  ?lexeme dct:language wd:Q25167 ;
    wikibase:lexicalCategory wd:Q24905 ;
    schema:dateModified ?lastModified .

  # MARK: Present Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?presentAndParticipleForm .
    ?presentAndParticipleForm ontolex:representation ?presentAndParticiple ;
      wikibase:grammaticalFeature wd:Q10345583 .
  }

  # MARK: Past Participle

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?pluralPastAndParticipleForm .
    ?pluralPastAndParticipleForm ontolex:representation ?pluralPastAndParticiple ;
      wikibase:grammaticalFeature wd:Q146786, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?definiteSingularPastAndParticipleForm .
    ?definiteSingularPastAndParticipleForm ontolex:representation ?definiteSingularPastAndParticiple ;
      wikibase:grammaticalFeature wd:Q53997851, wd:Q110786, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?neuterIndefiniteSingularPastAndParticipleForm .
    ?neuterIndefiniteSingularPastAndParticipleForm ontolex:representation ?neuterIndefiniteSingularPastAndParticiple ;
      wikibase:grammaticalFeature wd:Q1775461, wd:Q53997857, wd:Q110786, wd:Q12717679 .
  }

  OPTIONAL {
    ?lexeme ontolex:lexicalForm ?feminineMasculineIndefiniteSingularPastAndParticipleForm .
    ?feminineMasculineIndefiniteSingularPastAndParticipleForm ontolex:representation ?feminineMasculineIndefiniteSingularPastAndParticiple ;
      wikibase:grammaticalFeature wd:Q1775415, wd:Q499327, wd:Q53997857, wd:Q110786, wd:Q12717679 .
  }
}
